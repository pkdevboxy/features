defaultTasks 'useFeature'

subprojects {

    task useFeature {
        def readme = file('README.md')

        onlyIf {
            readme.exists()
        }

        doLast {
            readme.text = process(readme)
        }
    }
}

wrapper {
    // e.g.: `gradle wrapper -PlocalDistro=file:///path/to/gradle-<version>-bin.zip`
    if (project.hasProperty('localDistro')) {
        distributionUrl localDistro
    }
}

String process(File readme) {
    def tempReadme = createTempFile(readme)
    def writer = new PrintWriter(tempReadme)
    def gradleUserHome = createTempGradleUserHome()
    def handlingCommand = false

    readme.eachLine { line ->
        if (handlingCommand && !isCommandOutput(line)) {
            handlingCommand = false
            writer.println('')
        }
        if (!handlingCommand) {
            writer.println(line)
        }
        if (handlingCommand && isTotalTimeReport(line)) {
            writer.println(line)
        }
        if (isCommand(line)) {
            handlingCommand = true
            def command = stripCommandPrompt(line)
            if (isGradleCommand(command)) {
                command = configureGradleUserHome(command, gradleUserHome)
            }
            def bashCommand = wrapCommandWithBash(command)
            def process = bashCommand.execute(null, readme.getParentFile())
            def output = new StringBuffer()
            def skipDiffOutput = true
            process.waitForProcessOutput(output, output)
            new BufferedReader(new StringReader(output.toString())).with { reader ->
                def outputLine
                while ((outputLine = reader.readLine()) != null) {
                    if (isTotalTimeReport(outputLine) || isStartingNewDaemonReport(outputLine)) {
                        continue
                    }
                    if (isGitDiffCommand(command)) {
                        if (isFirstLineOfDiffContext(outputLine)) {
                            skipDiffOutput = false
                        }
                        if (skipDiffOutput) {
                            continue
                        }
                    }
                    if (isEmptyLine(outputLine)) {
                        writer.println('')
                    }
                    else if (containsFeaturesHome(outputLine)) {
                        writer.println("    ${stripFeaturesHome(outputLine)}")
                    }
                    else {
                        writer.println("    $outputLine")
                    }
                }
            }
        }
    }

    writer.close()
    gradleUserHome.deleteDir()
    return tempReadme.text
}

File createTempFile(File origFile) {
    File tempFile = File.createTempFile(origFile.name, '.tmp')
    tempFile.deleteOnExit()
    return tempFile
}

boolean isCommand(String line) {
    line.startsWith('    $ ')
}

List wrapCommandWithBash(String command) {
    ['bash', '-c', command]
}

String stripCommandPrompt(String line) {
    line.replace('    $ ', '')
}

File createTempGradleUserHome() {
   File.createTempDir('gradle-features', 'gradle-user-home')
}

boolean isGradleCommand(String command) {
    command =~ /gradlew/
}

String configureGradleUserHome(String command, File gradleUserHome) {
    "$command --gradle-user-home $gradleUserHome"
}

boolean isCommandOutput(String line) {
    line =~ /^$/ || line =~ /^    /
}

boolean isTotalTimeReport(String line) {
    line =~ /Total time: /
}

boolean isStartingNewDaemonReport(String line) {
    line =~ /Starting a new Gradle Daemon for this build/
}

Float extractTotalTime(String line) {
    Float.valueOf(line.replace('Total time: ', '').replace(' secs', ''))
}

boolean isEmptyLine(String line) {
    line =~ /^$/ || line =~ /^ +$/
}

boolean containsFeaturesHome(String line) {
    line.contains("${rootProject.rootDir}")
}

String stripFeaturesHome(String line) {
    line.replaceAll("${rootProject.rootDir}", '\\$FEATURES_HOME')
}

boolean isGitDiffCommand(String command) {
    command.startsWith('git diff')
}

boolean isFirstLineOfDiffContext(String line) {
    line.startsWith('@@')
}
